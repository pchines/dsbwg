#!/usr/bin/perl -w

use strict;
use warnings;
use Getopt::Long;
use Pod::Usage;
use File::Find;
use File::Spec;
our $VERSION = "0.4.1";
our %Opt;
our %Stats;     # counts and totals
our %User;      # map UIDs to usernames
our %Group;     # map GIDs to group names
our $AbsDir;    # top-level directory
our @Largest;   # largest files
our @Newest;    # most recently accessed files
our %Suffix;    # subtotals for each suffix: {suffix}[files,size]
our $SuffixRe;  # regex for suffixes to capture
our $RecentCutoff;  # epoch time minimum for "recent" files
our $Admin;     # boolean: indicates storage admin mode
# these directories must have absolute paths; don't end with slash
our $ARCH_DIR = "/mnt/archive_mount/data_tier_%s/archive";
our $PASSWD_DIR = "/mnt/archive_mount/passwd/%s_passwd";
our %LocalLookup;   # cache for Admin mode {host}{users|groups}{ID} => name

our $SymLinkFh; # file for symlink output
our $DirPermFh; # file for directory permissions
our $InfoFh;    # file for other output (summary)
our $DetailFh;  # file for file details (database)
our @DetailCols = qw(dir file mode user group size atime mtime);

our $TerminalAttached = -t STDOUT;
our $EMPTY = q{};
our $COMMA = q{,};
our $KILO = 1024;
our $Gzip = 'gzip';
our $GzipIn;
our $GzipOut;

=head1 NAME

archive_check - check directories to be archived

=head1 SYNOPSIS

Test specified directory tree for external symlinks and other issues, prior
to archiving data to tape:

  archive_check [options] DIR

For storage administrators, additional options (and information on disk)
is required:

  archive_check -ar 27 -host trek DIR

For complete documentation, run C<archive_check -man>

=head1 DESCRIPTION

Check directories to be archived for potential problems, including:

 - external symlinks
 - broken symlinks
 - recently accessed/changed files

Also reports total size and file owners, for planning and confirmation.
Generates a detailed database of all files unless the C<--nodetails> option
is specified.

=cut

#------------
# Begin MAIN 
#------------

process_commandline();
if ($Opt{prefix}) {
    $SymLinkFh = Open("$Opt{prefix}.link.txt$Opt{gz_all}", 'w');
    $DirPermFh = Open("$Opt{prefix}.dirs.txt$Opt{gz_big}", 'w');
    $InfoFh    = Open("$Opt{prefix}.info.txt$Opt{gz_all}", 'w');
    if ($Opt{details}) {
        $DetailFh  = Open("$Opt{prefix}.files.txt$Opt{gz_big}", 'w');
        print $DetailFh join("\t", @DetailCols), "\n";
    }
}
my %prefix;
for my $dir (@ARGV) {
    my $start_time = time;
    # clear data structures
    for my $i (qw(last_atime last_mtime dead_links external_links
                total_size files dirs)) {
        $Stats{$i} = 0;
    }
    %User = ();
    %Group = ();
    @Largest = ();
    @Newest = ();
    # evaluate each directory
    $AbsDir = File::Spec->rel2abs($dir);
    if (!$Opt{prefix}) {
        my ($v,$p,$d) = File::Spec->splitpath($AbsDir);
        if ($prefix{$d}) {
            my $n = 2;
            while ($prefix{"$d.$n"}) { ++$n; }
            $d = "$d.$n";
        }
        $SymLinkFh = Open("$Opt{outdir}$d.link.txt$Opt{gz_all}", 'w');
        $DirPermFh = Open("$Opt{outdir}$d.dirs.txt$Opt{gz_big}", 'w');
        $InfoFh    = Open("$Opt{outdir}$d.info.txt$Opt{gz_all}", 'w');
        if ($Opt{details}) {
            $DetailFh  = Open("$Opt{outdir}$d.files.txt$Opt{gz_big}", 'w');
            print $DetailFh join("\t", @DetailCols), "\n";
        }
    }
    find(\&check, $AbsDir);
    report_info($start_time);
}
if ($TerminalAttached) {
    print STDERR "\n";
}
warn "Grand total: ", human_readable($Stats{"grand_total"}), "\n";
for my $w (sort keys %{ $Stats{"warnings"} }) {
    warn "$w\n";
}
if ($Admin) {
    warn "Data written to prefix $Opt{prefix}\n";
}

#------------
# End MAIN
#------------

# receives $_ (filename), chdir-ed to enclosing dir
# $File::Find::dir  is path
# $File::Find::name is full path to file
sub check {
    for my $ex (@{ $Opt{exclude} }) {
        if ($File::Find::name =~ /^$ex/) {
            $File::Find::prune = 1;
            return;
        }
    }
    if (!defined $_) {
        return;
    }
    my $pre = $Opt{ignore};
    my $dir = $File::Find::dir;
    my $name = $File::Find::name;
    if (!$Opt{fullpaths}) {
        $dir =~ s{^(\Q$pre\E)?\Q$AbsDir\E}{.};
        $name = "$dir/$_";
    }
    if (-l) {
        my $loc = readlink;
        if (-e) {
            # check whether link is internal or external
            my $absloc = File::Spec->rel2abs($loc);
            if ($absloc !~ /^(\Q$pre\E)?\Q$AbsDir\E/) {
                print $SymLinkFh join("\t", "external", $name, $loc), "\n";
                # consider reporting size of external linked files
                # not sure what to do about dirs; total size of contents?
                ++$Stats{"external_links"};
            }
        }
        else {
            print $SymLinkFh join("\t", "dead", $name, $loc), "\n";
            ++$Stats{"dead_links"};
        }
        if ($DetailFh) {
            my @s = lstat;
            if (@s) {
                record_user_and_group(@s[4,5]);
                print $DetailFh join("\t", $dir, $_,
                    sprintf("%06o", $s[2]), $User{$s[4]},
                    $Group{$s[5]}, $s[7], iso_datetime($s[8]),
                    iso_datetime($s[9])), "\n";
            }
            else {
                warn "WARNING: could not lstat $File::Find::name\n";
                add_warning("Could not retrieve info for one or more files",
                        $InfoFh);
            }
        }
        return;
    }
    my @s = stat;
    if (!@s) {
        warn "WARNING: could not stat $File::Find::name\n";
        add_warning("Could not retrieve info for one or more files", $InfoFh);
        return;
    }
    record_user_and_group(@s[4,5]);
    if (-d _) {
        # record permissions for directory, so can be reconstituted
        print $DirPermFh join("\t", "$dir/$_",
                sprintf("%04o", $s[2] & 07777),
                $User{$s[4]}, $Group{$s[5]}, @s[8,9]), "\n";
        ++$Stats{dirs};
        if ($TerminalAttached && $Stats{dirs} % 100 == 0) {
            print STDERR ".";
        }
    }
    else {
        ++$Stats{files};
        if ($DetailFh) {
            print $DetailFh join("\t", $dir, $_,
                    sprintf("%06o", $s[2]), $User{$s[4]},
                    $Group{$s[5]}, $s[7], iso_datetime($s[8]),
                    iso_datetime($s[9])), "\n";
        }
        if ($Opt{largest} &&
                (@Largest < $Opt{largest} || $s[7] > $Largest[-1][0])) {
            insert_largest(\@Largest, [$s[7], $name], $Opt{largest});
        }
        $Stats{"total_size"} += $s[7];
        if ($Opt{newest} && $s[8] > $RecentCutoff &&
                (@Newest < $Opt{newest} || $s[8] > $Newest[-1][0])) {
            insert_largest(\@Newest, [$s[8], $name], $Opt{newest});
        }
        # check last accessed/changed date
        if ($Stats{"last_atime"} < $s[8]) {
            $Stats{"last_atime"} = $s[8];
        }
        if ($Stats{"last_mtime"} < $s[9]) {
            $Stats{"last_mtime"} = $s[9];
        }
        if ($SuffixRe && /$SuffixRe/) {
            $Suffix{$1}[0] += 1;
            $Suffix{$1}[1] += $s[7];
        }
    }
}

sub insert_largest {
    my ($ra_large, $ra_file, $n) = @_;
    my $j;
    for (my $i = 0; $i < @$ra_large; ++$i) {
        if ($ra_file->[0] > $ra_large->[$i][0]) {
            $j = $i;
            last;
        }
    }
    if (defined $j) {
        splice @$ra_large, $j, 0, $ra_file;
    }
    else {
        push @$ra_large, $ra_file;
    }
    if (@$ra_large > $n) {
        pop @$ra_large;
    }
}

sub iso_datetime {
    my ($e) = @_;
    my @t = $Opt{utc} ? gmtime($e) : localtime($e);
    return sprintf("%d-%02d-%02dT%02d:%02d:%02d", $t[5]+1900, $t[4]+1, $t[3],
            $t[2], $t[1], $t[0]);
}

sub record_user_and_group {
    my ($uid, $gid) = @_;
    if (!exists $User{$uid}) {
        if ($Admin) {
            my $rh = read_passwd_file($Opt{host});
            $User{$uid} = $rh->{$uid};
        }
        else {
            ($User{$uid}) = getpwuid($uid);
        }
        if (!defined $User{$uid}) {
            $User{$uid} = $uid;
        }
    }
    if (!exists $Group{$gid}) {
        if ($Admin) {
            my $rh = read_group_file($Opt{host});
            $Group{$gid} = $rh->{$gid};
        }
        else {
            ($Group{$gid}) = getgrgid($gid);
        }
        if (!defined $Group{$gid}) {
            $Group{$gid} = $gid;
        }
    }
}

sub human_readable {
    my ($val, $fmt) = @_;
    $fmt ||= "%.2f";
    my $suffix = $EMPTY;
    if ($val > $KILO) {
        $val /= $KILO;
        $suffix = "K";
    }
    if ($val > $KILO) {
        $val /= $KILO;
        $suffix = "M";
    }
    if ($val > $KILO) {
        $val /= $KILO;
        $suffix = "G";
    }
    if ($val > $KILO) {
        $val /= $KILO;
        $suffix = "T";
    }
    if ($val > $KILO) {
        $val /= $KILO;
        $suffix = "P";
    }
    return sprintf "$fmt\%s", $val, $suffix;
}

sub report_info {
    my ($start) = @_;
    my $end = time;
    $Stats{"grand_total"} += $Stats{"total_size"};
    my $size = human_readable($Stats{"total_size"});
    if ($Opt{si}) {
        $size .= " (reported in SI units, not binary)";
    }
    my $atime = $Opt{utc} ? gmtime($Stats{"last_atime"}) . " UTC"
                          : localtime($Stats{"last_atime"});
    my $mtime = $Opt{utc} ? gmtime($Stats{"last_mtime"}) . " UTC"
                          : localtime($Stats{"last_mtime"});
    my $stime = $Opt{utc} ? gmtime($start) . " UTC" : localtime($start);
    my $etime = $Opt{utc} ? gmtime($end) . " UTC" : localtime($end);
    my $elapsed = $end - $start;
    my $host = $ENV{HOSTNAME} || $ENV{HOST} || $ENV{COMPUTERNAME} || $EMPTY;
    my $dblink = $EMPTY;
    if ($Admin) {
        ($host, $AbsDir) = use_mount_info($host, $AbsDir);
        $dblink = "\n\tArchive request ID:\t$Opt{archive_id}";
    }
    print $InfoFh <<"END_SUMMARY";
Pre-archive report for $host:$AbsDir$dblink
\tReport started :\t$stime
\tReport finished:\t$etime
\tElapsed (seconds):\t$elapsed
\tTotal size of files:\t$size
\tTotal number of files:\t$Stats{files}
\tTotal number of dirs:\t$Stats{dirs}
\tLatest access time:\t$atime
\tLatest modify time:\t$mtime
\tNumber of dead links:\t$Stats{"dead_links"}
\tNo. of external links:\t$Stats{"external_links"}

END_SUMMARY
    if ($SuffixRe) {
        print $InfoFh "Files with specified suffixes:\n";
        my $n = 0;
        my $t = 0;
        for my $suf (sort keys %Suffix) {
            printf $InfoFh "\t%s:\t%d files, %s (%.1f%%)\n",
                   $suf, $Suffix{$suf}[0], human_readable($Suffix{$suf}[1]),
                   100 * $Suffix{$suf}[1] / $Stats{"total_size"};
            $n += $Suffix{$suf}[0];
            $t += $Suffix{$suf}[1];
        }
        printf $InfoFh "\ttotal:\t%d files, %s (%.1f%%)\n\n",
               $n, human_readable($t), 100 * $t / $Stats{"total_size"};
    }
    print $InfoFh "Users who own files/directories in this tree:\n";
    for my $u (sort values %User) {
        print $InfoFh "\t$u\n";
    }
    print $InfoFh "Groups who can access files/directories in this tree:\n";
    for my $g (sort values %Group) {
        print $InfoFh "\t$g\n";
    }
    print $InfoFh "\n";
    if ($Stats{"dead_links"}) {
        add_warning("Dead symlinks were found", $InfoFh);
    }
    if ($Stats{"external_links"}) {
        add_warning("Symlinks to external files were found", $InfoFh);
    }
    if ($Stats{"last_atime"} > $RecentCutoff) {
        add_warning("Some files have been accessed recently", $InfoFh);
    }
    if ($Stats{"last_mtime"} > $RecentCutoff) {
        add_warning("Some files have been modified recently", $InfoFh);
    }
    if ($Opt{newest} && @Newest) {
        print $InfoFh scalar(@Newest), " most recently accessed files:\n";
        my $pre = $Opt{ignore};
        for my $ra (@Newest) {
            $ra->[1] =~ s{^(\Q$pre\E)?\Q$AbsDir\E/?}{./};
            print $InfoFh "\t", iso_datetime($ra->[0]), "\t", $ra->[1], "\n";
        }
    }
    if ($Opt{largest}) {
        print $InfoFh scalar(@Largest), " largest files in this tree:\n";
        my $pre = $Opt{ignore};
        for my $ra (@Largest) {
            $ra->[1] =~ s{^(\Q$pre\E)?\Q$AbsDir\E/?}{./};
            printf $InfoFh "\t%8s  %s\n", human_readable($ra->[0]), $ra->[1];
        }
    }
}

sub get_mount {
    my ($p) = @_;
    my ($mh, $ex, $mp);
    my @o = `df -h $p`;
    if ($o[1] =~ /^([\w\.-]+):(\S+)/) {
        $mh = $1;
        $ex = $2;
        if ($o[1] =~ /(\S+)$/) {
            $mp = $1;
        }
    }
    return ($mh, $ex, $mp);
}

sub use_mount_info {
    my ($h, $p) = @_;
    my ($real_h, $export, $mount) = get_mount($p);
    if (!$mount) {
        warn "Can't find local mount point matching $p\n";
    }
    elsif ($p !~ s/^$mount/$export/) {
        warn "Error translating path based on $mount = $export\n";
    }
    else {
        $h = $real_h;
        $h =~ s/-(sc|ba)\.nhgri\.nih\.gov$//i;
    }
    return ($h, $p);
}

sub add_warning {
    my ($msg, $fh) = @_;
    ++$Stats{"warnings"}{$msg};
    if ($fh) {
        print $fh "WARNING: ", $msg, "\n";
    }
}

# cut and pasted from GTB::File, to avoid having non-core dependencies
# then: s/croak/die/ (all of the "or die" were originally die, others croak)
sub Open {
    my ($file, $mode) = @_;
    if (!$file && $file ne '0') {
        die "Open: no filename provided";
    }
    if ($mode) {
        $mode = lc $mode;
    }
    elsif ($file =~ /^\s*\|/) {
        $mode = 'w';
    }
    else {
        $mode = 'r';
    }
    my $fh;
    if ($file =~ /\|/) {
        if ($mode eq 'r') {
            if ($file =~ /\|\s*$/) {
                open $fh, $file or die "Can't open pipe '$file', $!\n";
            }
            else {
                die "To open pipe for reading, pipe character must "
                    . "appear at end of command";
            }
        }
        elsif ($mode eq 'w') {
            if ($file =~ /^\s*\|/) {
                open $fh, $file or die "Can't open pipe '$file', $!\n";
            }
            else {
                die "To open pipe for writing, pipe character must "
                    . "appear at beginning of command";
            }
        }
        else { # pipe, but not first or last in sequence
            die << "END_MSG";
If a pipe character is present in the open string, there must be a pipe at
the beginning or end of the string, depending upon whether you plan to
write or read to the filehandle; '$file' is not valid.  If you need to read
and write to a program, try IPC::Open2 or IPC::Open3.
END_MSG
        }
    }
    elsif ($file =~ /\.(b?gz|bz2|zip|Z)$/) {
        if ($mode eq 'r') {
            my $prog = $1 eq 'bz2' ? 'bzip2' : ($GzipIn || $Gzip);
            die "File ($file) not found" unless (-e $file);
            die "File ($file) was not readable" unless (-r $file);
            open $fh, "$prog -dc $file |"
                or die "Can't read $file with $prog, $!\n";
        }
        elsif ($mode eq 'w') {
            my $prog = $1 eq 'bz2' ? 'bzip2' : ($GzipOut || $Gzip);
            open $fh, "| $prog > $file"
                or die "Can't create $prog file $file, $!\n";
        }
        elsif ($mode eq 'a') {
            if ($1 eq 'bz2') {
                die "Open: mode 'a' not supported for bzip2 file $file";
            }
            my $prog = $GzipOut || $Gzip;
            open $fh, "| $prog >> $file"
                or die "Can't append $prog output to $file, $!\n";
        }
        else {
            die "Open: mode '$mode' not supported; use 'r', 'w' or 'a'";
        }
    }
    elsif ($file eq '-') {
        if ($mode eq 'r') {
            open $fh, '-' or die "Can't read from STDIN, $!\n";
        }
        elsif ($mode eq 'w' || $mode eq 'a') {
            open $fh, '>-' or die "Can't write to STDOUT, $!\n";
        }
        else {
            die "Open: mode '$mode' not supported; use 'r', 'w' or 'a'";
        }
    }
    elsif ($mode eq 'r') {
        open $fh, '<', $file or die "Can't open $file, $!\n";
    }
    elsif ($mode eq 'w') {
        open $fh, '>', $file or die "Can't create $file, $!\n";
    }
    elsif ($mode eq 'a') {
        open $fh, '>>', $file or die "Can't append to $file, $!\n";
    }
    else {
        die "Open: mode '$mode' not supported; use 'r', 'w' or 'a'";
    }
    return $fh;
}

sub read_passwd_file {
    my ($host) = @_;
    if (!$LocalLookup{$host}{users}) {
        my $f = sprintf("$PASSWD_DIR/passwd", $host);
        if (! -f $f) {
            warn "Warning: did not find passwd file for $host\n";
            return {};
        }
        my %user;
        my $fh = Open($f);
        while (<$fh>) {
            chomp;
            my @f = split /:/;
            $user{$f[2]} = $f[0];
        }
        $LocalLookup{$host}{users} = \%user;
    }
    return $LocalLookup{$host}{users};
}

sub read_group_file {
    my ($host) = @_;
    if (!$LocalLookup{$host}{groups}) {
        my $f = sprintf("$PASSWD_DIR/group", $host);
        if (! -f $f) {
            warn "Warning: did not find group file for $host\n";
            return {};
        }
        my %group;
        my $fh = Open($f);
        while (<$fh>) {
            chomp;
            my @f = split /:/;
            $group{$f[2]} = $f[0];
        }
        $LocalLookup{$host}{groups} = \%group;
    }
    return $LocalLookup{$host}{groups};
}

sub process_commandline {
    # Set defaults here
    %Opt = (details => 1,
            exclude => [],
            ignore  => $EMPTY,
            largest => 5,
            newest  => 5,
            recent  => 30,  # days
            suffixes=> [],
            );
    GetOptions(\%Opt, qw(archive_id=i details! exclude=s fullpaths gzip!
                host=s ignore=s largest=i newest=i output|outdir|prefix=s
                recent=i si suffixes=s utc
                manual help+ version)) || pod2usage(0);
    if ($Opt{manual})  { pod2usage(verbose => 2); }
    if ($Opt{help})    { pod2usage(verbose => $Opt{help}-1); }
    if ($Opt{version}) { die "archive_check, version $VERSION\n"; }
    # If non-option arguments are required, uncomment next line
    if (!@ARGV) {
        pod2usage("Expected directory name(s)");
    }
    my @nd = grep { !-d } @ARGV;
    if (@nd) {
        warn "Argument '$_' is not a directory\n" for @nd;
        #pod2usage("All arguments must be existing directories");
    }
    # Add criteria to test option arguments below
    if ($Opt{output}) {
        if (-d $Opt{output}) {
            $Opt{outdir} = $Opt{output};
            $Opt{outdir} =~ s{/*$}{/};
        }
        else {
            $Opt{prefix} = $Opt{output};
            (undef, $Opt{outdir}, undef) = File::Spec->splitpath($Opt{prefix});
            if (@ARGV > 1) {
                $Opt{fullpaths} = 1;
            }
        }
    }
    if ($Opt{archive_id} && $Opt{host}) {
        $Admin = 1;
    }
    elsif ($Opt{archive_id} || $Opt{host}) {
        pod2usage("Administrative mode requires both -archive_id and -host");
    }
    if ($Admin) {
        if (@ARGV != 1) {
            pod2usage("Administrative mode allows only one directory");
        }
        my ($i) = use_mount_info($ENV{HOSTNAME}, $ARGV[0]);
        $Opt{outdir} = sprintf("$ARCH_DIR/AR$Opt{archive_id}/", $i);
        mkdir $Opt{outdir};
        $Opt{prefix} = $Opt{outdir}."AR$Opt{archive_id}";
    }
    else {
        $Opt{outdir} ||= "./";
    }
    if (!-e $Opt{outdir}) {
        die "Output directory '$Opt{outdir}' does not exist; please create"
            . " it first.\n";
    }
    if (!-w $Opt{outdir}) {
        die "You do not have permission to write to output directory\n"
            . "'$Opt{outdir}'; please specify a different location using"
            . " the -o option\n";
    }
    $Opt{ignore} =~ s{/*$}{};
    for (@{ $Opt{exclude} }) {
        $_ = File::Spec->rel2abs($_);
        # end without slash
        s{/*$}{};
    }
    $Opt{gz_big} = ".gz";
    $Opt{gz_all} = $EMPTY;
    if ($Opt{gzip}) {
        $Opt{gz_all} = ".gz";
    }
    elsif (defined $Opt{gzip}) {
        $Opt{gz_big} = $EMPTY;
    }
    if ($Opt{si}) {
        $KILO = 1000;
    }
    if (@{ $Opt{suffixes} }) {
        my @suf = map { $_ eq "+ALL" ? '[^.]+'
                      : $_ eq "+DOC" ? qw(doc xls ppt docx xlsx pptx pdf)
                      : $_ eq "+IMG" ? qw(bpm gif jpg jpeg png tif tiff)
                      : $_ eq "+Z" ? qw(gz bz2 zip Z 7z)
                      : (split $COMMA) } @{ $Opt{suffixes} };
        my $suf = join '|', @suf;
        $SuffixRe = qr/\.($suf)$/o;
    }
    # convert "recent" days to epoch seconds minimum cutoff
    $RecentCutoff = time - 60 * 60 * 24 * $Opt{recent};
}

__END__

=head1 OPTIONS

=over 4

=item B<--exclude> DIR

Exclude the given subdirectory.  This option can be used multiple times.

=item B<--fullpaths>

Write full paths to details file; this is automatically done when multiple
directories are scanned, and the results are written to a single file.
Otherwise, it should probably be avoided, as it uses extra space, makes
comparison more difficult, and doesn't add any information.

=item B<--gzip>

=item B<--nogzip>

Specify whether output files should all be gzip compressed or not.  By
default, the files that are likely to become large are gzipped, and those
that are likely to be small are not.

=item B<--largest> 5

Show the largest N files in summary (info.txt).  Use 0 to disable.

=item B<--nodetails>

By default, a "PREFIX.files.txt.gz" file is written, with details including
file directory, name, size, permissions and access/modify times will be
written for each file.  Times will be in local timezone unless C<--utc>
option is used.  Use the C<--nodetails> option to disable writing this file.

=item B<--newest> 5

Show the most recently accessed (modified) N files in the summary report.
Only "recent" files are considered.  See C<--recent>.  Use 0 to disable.

=item B<--output> DIR

=item B<--output> PREFIX

Destination directory or prefix (may contain a path) for output files.
Default is to write to current directory, using the basename of each
directory scanned as a prefix.

=item B<--recent> 30

If any file has been accessed or modified within this number of days, issue a
warning.  If C<--newest> is greater than zero, report specific files.

=item B<--si>

Use metric SI units (K=1000), rather than binary (K=1024).

=item B<--suffixes> xls,csv,txt

=item B<--suffixes> +ALL | +DOC | +IMG | +Z

Specific file types (indicated by filename .suffix) to track and subtotal.
Special values allow commonly-specified suffixes to be easily identified:

    +ALL - any file with a suffix
    +DOC - documents: doc,xls,ppt,docx,xlsx,pptx,pdf
    +IMG - images: bpm,gif,jpg,jpeg,png,tif,tiff
    +Z   - compressed: 7z,bz2,gz,zip,Z

This option can be used multiple times, or multiple suffixes can be separated
with commas.  When special values are used, they cannot be comma-separated.

=item B<--utc>

Write time stamps in universal (GMT, aka UTC) time, rather than local
timezone.

=item B<--help|--manual>

Display documentation.  One C<--help> gives a brief synopsis, C<-h -h> shows
all options, C<--manual> provides complete documentation.

=head2 OPTIONS FOR ADMIN MODE ONLY:

=item B<--archive_id> #

Required: archive request that is referenced.  Written to info file, and forms
the basis for output file names.

=item B<--host> HOSTNAME

Required: Linux host on which the user has mounted the directory.  This
hostname (simple, not fully qualified) will be use to lookup usernames in the
passwd files.

=back

=head1 FILES

The following output files are generated:

=over 4

=item PREFIX.info.txt

Summary information, including total size, last modify/access time, file
owners and groups, and largest N files.

=item PREFIX.link.txt

List of external and broken symlinks.

=item PREFIX.dirs.txt.gz

Permissions and ownership for each directory (because these are not preserved in our tape archives).  Access and modify times are given in epoch seconds.

=item PREFIX.files.txt.gz

Directory, filename, permissions, owner, group, size, and modify/access times
for each file and directory.

=back

=head1 AUTHOR

 Peter Chines - pchines@mail.nih.gov

=head1 LEGAL

This software/database is "United States Government Work" under the terms of
the United States Copyright Act.  It was written as part of the authors'
official duties for the United States Government and thus cannot be
copyrighted.  This software/database is freely available to the public for
use without a copyright notice.  Restrictions cannot be placed on its present
or future use. 

Although all reasonable efforts have been taken to ensure the accuracy and
reliability of the software and data, the National Human Genome Research
Institute (NHGRI) and the U.S. Government does not and cannot warrant the
performance or results that may be obtained by using this software or data.
NHGRI and the U.S.  Government disclaims all warranties as to performance,
merchantability or fitness for any particular purpose. 

In any work or product derived from this material, proper attribution of the
authors as the source of the software or data should be made, using "NHGRI
Information Technology Branch" as the citation. 

=cut
